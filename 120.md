Low Bamboo Rhino

medium

# `claimMultipleStaking` may out of gas

## Summary
The `claimMultipleStaking` function in the `SdtRewardReceiver` contract may encounter an "out of gas" error due to gas exhaustion.

## Vulnerability Detail
The `claimMultipleStaking()` function performs multiple iterations and operations, potentially involving a large number of rewards extraction operations. The gas cost of executing the function may exceed the gas limit of the current block. If the gas cost exceeds the remaining gas limit, the transaction will fail due to gas exhaustion.

## Impact
If the gas cost of claimMultipleStaking exceeds the available gas limit, the function will stop execution and fail to complete the intended operations. This can prevent users from claiming their rewards and potentially disrupt the expected functionality of the contract.


## Code Snippet
https://github.com/sherlock-audit/2023-11-convergence/blob/main/sherlock-cvg/contracts/Staking/StakeDAO/SdtRewardReceiver.sol#L119-L203

```solidity
function claimMultipleStaking(
        ISdtStakingPositionManager.ClaimSdtStakingContract[] calldata claimContracts,
        bool isConvert,
        bool isMint,
        uint256 sdtRewardCount
    ) external {
        /// @dev Checks for all positions input in data : Token ownership & verify positions are linked to the right staking service & verify timelocking
        sdtStakingPositionManager.checkMultipleClaimCompliance(claimContracts, msg.sender);

        /// @dev Accumulates amounts of CVG coming from all claims.
        uint256 _totalCvgClaimable;

        /// @dev Array merging & accumulating rewards coming from diferent claims.
        ICommonStruct.TokenAmount[] memory _totalSdtClaimable = new ICommonStruct.TokenAmount[](sdtRewardCount);

        /// @dev Iterate over all staking service
        for (uint256 stakingIndex; stakingIndex < claimContracts.length; ) {
            ISdtStakingPositionService sdtStaking = claimContracts[stakingIndex].stakingContract;
            uint256 tokensLength = claimContracts[stakingIndex].tokenIds.length;
            /// @dev Iterate over all tokens linked to the iterated cycle.
            for (uint256 tokenIdIndex; tokenIdIndex < tokensLength; ) {
                /** @dev Claims Cvg & Sdt
                 *       Returns the amount of CVG claimed on the position.
                 *       Returns the array of all SDT rewards claimed on the position.
                 */
                (uint256 cvgClaimable, ICommonStruct.TokenAmount[] memory _sdtRewards) = sdtStaking.claimCvgSdtMultiple(
                    claimContracts[stakingIndex].tokenIds[tokenIdIndex],
                    msg.sender
                );
                /// @dev increments the amount to mint at the end of function
                _totalCvgClaimable += cvgClaimable;

                uint256 sdtRewardsLength = _sdtRewards.length;
                /// @dev Iterate over all SDT rewards claimed on the iterated position
                for (uint256 positionRewardIndex; positionRewardIndex < sdtRewardsLength; ) {
                    /// @dev Is the claimable amount is 0 on this token
                    ///      We bypass the process to save gas
                    if (_sdtRewards[positionRewardIndex].amount != 0) {
                        /// @dev Iterate ower the final array to merge the iterated SdtRewards in the totalSdtClaimable
                        for (uint256 totalRewardIndex; totalRewardIndex < sdtRewardCount; ) {
                            address iteratedTotatClaimableToken = address(_totalSdtClaimable[totalRewardIndex].token);
                            /// @dev If the token is not already in the totalSdtClaimable.
                            if (iteratedTotatClaimableToken == address(0)) {
                                /// @dev Push the token in the totalClaimable array.
                                _totalSdtClaimable[totalRewardIndex] = ICommonStruct.TokenAmount({
                                    token: _sdtRewards[positionRewardIndex].token,
                                    amount: _sdtRewards[positionRewardIndex].amount
                                });
                                /// @dev Pass to the next token
                                break;
                            }
                            /// @dev If the token is already in the totalSdtClaimable.
                            if (iteratedTotatClaimableToken == address(_sdtRewards[positionRewardIndex].token)) {
                                /// @dev Increments the claimable amount.
                                _totalSdtClaimable[totalRewardIndex].amount += _sdtRewards[positionRewardIndex].amount;
                                /// @dev Pass to the next token
                                break;
                            }

                            /// @dev If the token is not found in the totalRewards and we are at the end of the array.
                            ///      it means the sdtRewardCount is not properly configured.
                            require(totalRewardIndex != sdtRewardCount - 1, "REWARD_COUNT_TOO_SMALL");

                            unchecked {
                                ++totalRewardIndex;
                            }
                        }
                    }

                    unchecked {
                        ++positionRewardIndex;
                    }
                }

                unchecked {
                    ++tokenIdIndex;
                }
            }
            unchecked {
                ++stakingIndex;
            }
        }

        _withdrawRewards(msg.sender, _totalCvgClaimable, _totalSdtClaimable, isConvert, isMint);
    }
```

## Tool used

Manual Review

## Recommendation

1.Consider breaking down the operations into smaller batches to reduce the computational and data processing load within a single function call.
2.Optimize the logic of loops and iterations to minimize redundant calculations and data operations.