Dazzling Frost Poodle

high

# The claimRewards function may be subject to reentrancy attacks

## Summary
The `claimRewards` function not comply with the `Checks-Effects-Interactions` principle, and may be subject to reentrancy attacks.

## Vulnerability Detail
The `claimRewards` function in `YsDistributor.sol` claims the associated rewards to the locking position NFT. It first check `rewardsClaimedForToken[tokenId][tdeId]` is not `true`, then call `_claimTokenRewards` function to claim token rewards and finally change the `rewardsClaimedForToken[tokenId][tdeId]` to `true`. It not comply with the `Checks-Effects-Interactions` principle and may be subject to reentrancy attacks.

```solidity
YsDistributor.sol
    function claimRewards(uint256 tokenId, uint256 tdeId, address receiver, address operator) external {
        [...]

        /// @dev Cannot claim twice rewards for a TDE.
        require(!rewardsClaimedForToken[tokenId][tdeId], "ALREADY_CLAIMED"); // <- check
        
        [...]

        /// @dev Claim according token rewards.
        _claimTokenRewards(tokenId, tdeId, share, receiver); // <- here

        /// @dev Mark the TDE id for this token as claimed on the Storage.
        rewardsClaimedForToken[tokenId][tdeId] = true; // <- change the status
    }

    function _claimTokenRewards(uint256 tokenId, uint256 tdeId, uint256 share, address receiver) internal {
        address[] memory tokens = depositedTokenAddressForTde[tdeId];
        TokenAmount[] memory tokensClaimable = new TokenAmount[](tokens.length);

        for (uint256 i; i < tokens.length; ) {
            IERC20 _token = IERC20(tokens[i]);
            uint256 _amountUser = _calculateUserRewardAmount(tdeId, _token, share);

            tokensClaimable[i] = TokenAmount({token: _token, amount: _amountUser});

            _token.safeTransfer(receiver, _amountUser); // <- transfer before change the status

            unchecked {
                ++i;
            }
        }

        emit TokensClaim(tokenId, tdeId, share, tokensClaimable);
    }
```

## Impact
The claimRewards function may be subject to reentrancy attacks

## Code Snippet
https://github.com/sherlock-audit/2023-11-convergence/blob/main/sherlock-cvg/contracts/Rewards/YsDistributor.sol#L155-L195
https://github.com/sherlock-audit/2023-11-convergence/blob/main/sherlock-cvg/contracts/Rewards/YsDistributor.sol#L205-L220


## Tool used
Manual Review

## Recommendation
Comply with the `Checks-Effects-Interactions` principle, change the status first or use `noReentrancy` modifier.
```solidity
YsDistributor.sol
    function claimRewards(uint256 tokenId, uint256 tdeId, address receiver, address operator) external {
        [...]

        /// @dev Cannot claim twice rewards for a TDE.
        require(!rewardsClaimedForToken[tokenId][tdeId], "ALREADY_CLAIMED"); // <- check
        
        [...]

        /// @dev Mark the TDE id for this token as claimed on the Storage.
        rewardsClaimedForToken[tokenId][tdeId] = true; // <- change the status first

        /// @dev Claim according token rewards.
        _claimTokenRewards(tokenId, tdeId, share, receiver); // <- transfer

    }
```
