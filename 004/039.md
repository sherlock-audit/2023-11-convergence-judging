Refined Brick Hare

high

# Invariant Violation: `LockingPositionManager.sol#manageOwnedAndDelegated` `OwnedAndDelegated` properties are not collision-resistant.

## Summary

Due to missing validation rules, calls to [`manageOwnedAndDelegated`](https://github.com/sherlock-audit/2023-11-convergence/blob/e894be3e36614a385cf409dc7e278d5b8f16d6f2/sherlock-cvg/contracts/Locking/LockingPositionDelegate.sol#L330C14-L330C37) on the `LockingPositionManager` will allow any `external` caller to specify `_ownedAndDelegatedTokens` calldata that can contain duplicate token identifiers, provided the caller is indeed the owner of these tokens.

## Vulnerability Detail

When updating the [`tokenOwnedAndDelegated`](https://github.com/sherlock-audit/2023-11-convergence/blob/e894be3e36614a385cf409dc7e278d5b8f16d6f2/sherlock-cvg/contracts/Locking/LockingPositionDelegate.sol#L52) mapping via [`manageOwnedAndDelegated`](https://github.com/sherlock-audit/2023-11-convergence/blob/e894be3e36614a385cf409dc7e278d5b8f16d6f2/sherlock-cvg/contracts/Locking/LockingPositionDelegate.sol#L330C14-L330C37), a malicious caller is permitted to pass an arbitrary calldata value of `OwnedAndDelegated`.

The `OwnedAndDelegated` calldata struct corresponds to [three caller-defined `uint256[]` arrays](https://github.com/sherlock-audit/2023-11-convergence/blob/e894be3e36614a385cf409dc7e278d5b8f16d6f2/sherlock-cvg/contracts/Locking/LockingPositionDelegate.sol#L29C1-L33C6). The contents of these arrays are evaluated to determine the `owneds`, `mgDelegateds` and `veDelegateds` to be processed on behalf of the `msg.sender`.

As shown below, when interpreting the contents of these arrays, the `LockingPositionDelegate` only cares to ensure the caller is indeed the owner of these tokens, and not whether these tokens have been processed by a previous loop iteration:

```solidity
/**
 * @notice Allow a user to manage the tokens id (owned and delegated) used to represent their voting power.
 * @dev This prevents bad actors who will spam an address by transferring or delegating a lot of VE/MG positions.
 * | This will prevent the oog when the voting/metagovernance power is calculated.
 * @param _ownedAndDelegatedTokens array of owned/veDelegated/mgDelegated tokenIds allowed
 */
function manageOwnedAndDelegated(OwnedAndDelegated calldata _ownedAndDelegatedTokens) external {
    /** @dev Clear the struct owneds and delegateds tokenId allowed for this user.*/
    delete tokenOwnedAndDelegated[msg.sender];

    /** @dev Add new owned tokenIds allowed for this user.*/
    for (uint256 i; i < _ownedAndDelegatedTokens.owneds.length;) { /// @audit i.e. [69, 69, 69]
        /** @dev Check if tokenId is owned by the user.*/
        require(
            msg.sender == cvgControlTower.lockingPositionManager().ownerOf(_ownedAndDelegatedTokens.owneds[i]),
            "TOKEN_NOT_OWNED"
        );
        tokenOwnedAndDelegated[msg.sender].owneds.push(_ownedAndDelegatedTokens.owneds[i]); /// @audit
        unchecked {
            ++i;
        }
    }
    /** @dev Add new mgCvg delegated tokenIds allowed for this user.*/
    for (uint256 i; i < _ownedAndDelegatedTokens.mgDelegateds.length;) {
        /** @dev Check if the user is a mgCvg delegatee for this tokenId.*/
        (, , uint256 _toIndex) = getMgDelegateeInfoPerTokenAndAddress(
            _ownedAndDelegatedTokens.mgDelegateds[i],
            msg.sender
        );
        require(_toIndex != 999, "NFT_NOT_MG_DELEGATED");
        tokenOwnedAndDelegated[msg.sender].mgDelegateds.push(_ownedAndDelegatedTokens.mgDelegateds[i]); /// @audit
        unchecked {
            ++i;
        }
    }
    /** @dev Add new veCvg delegated tokenIds allowed for this user.*/
    for (uint256 i; i < _ownedAndDelegatedTokens.veDelegateds.length;) {
        /** @dev Check if the user is the veCvg delegatee for this tokenId.*/
        require(msg.sender == delegatedVeCvg[_ownedAndDelegatedTokens.veDelegateds[i]], "NFT_NOT_VE_DELEGATED");
        tokenOwnedAndDelegated[msg.sender].veDelegateds.push(_ownedAndDelegatedTokens.veDelegateds[i]); /// @audit
        unchecked {
            ++i;
        }
    }
}
```

As we can see in all three instances, provided the `msg.sender` has sufficient access control, they can create **arbitrarily long arrays of duplicate data** and cache these as their current signalling `tokenOwnedAndDelegated[msg.sender]`.

## Impact

1. When coupled with [`LockingPositionService#mgCvgVotingPowerPerAddress`](https://github.com/sherlock-audit/2023-11-convergence/blob/e894be3e36614a385cf409dc7e278d5b8f16d6f2/sherlock-cvg/contracts/Locking/LockingPositionService.sol#L773), it can be demonstrated that a user's voting power can be gamed through this manipulation:

```solidity
(uint256[] memory tokenIdsOwneds, uint256[] memory tokenIdsDelegateds) = _lockingPositionDelegate
    .getTokenMgOwnedAndDelegated(_user);

/** @dev Sum voting power from delegated (allowed) tokenIds to _user. */
for (uint256 i; i < tokenIdsDelegateds.length; ) {
    uint256 _tokenId = tokenIdsDelegateds[i];
    (uint256 _toPercentage, , uint256 _toIndex) = _lockingPositionDelegate.getMgDelegateeInfoPerTokenAndAddress(
        _tokenId,
        _user
    );
    /** @dev Check if is really delegated, if not mg voting power for this tokenId is 0. */
    if (_toIndex < 999) {
        uint256 _tokenBalance = balanceOfMgCvg(_tokenId);
        _totalMetaGovernance += (_tokenBalance * _toPercentage) / MAX_PERCENTAGE;
    }

    unchecked {
        ++i;
    }
}
```

As you can see, since we allow the arrays of `tokenIdsOwneds` and `tokenIdsDelegateds` to grow unbounded, this exploit has the ability to undermine the maximum `percentage` allocation for a single token, which could be used to drastically amplify voting power.

2. A similar error takes place in [`LockingPositionService#veCvgVotingPowerPerAddress`](https://github.com/sherlock-audit/2023-11-convergence/blob/e894be3e36614a385cf409dc7e278d5b8f16d6f2/sherlock-cvg/contracts/Locking/LockingPositionService.sol#L727), where again the accumulated `_totalVotingPower` can be gamed through duplicate array contents:

```solidity
function veCvgVotingPowerPerAddress(address _user) external view returns (uint256) {
    uint256 _totalVotingPower;

    ILockingPositionDelegate _lockingPositionDelegate = cvgControlTower.lockingPositionDelegate();

    (uint256[] memory tokenIdsOwneds, uint256[] memory tokenIdsDelegateds) = _lockingPositionDelegate
        .getTokenVeOwnedAndDelegated(_user);

    /** @dev Sum voting power from delegated tokenIds to _user. */
    for (uint256 i; i < tokenIdsDelegateds.length; ) {
        uint256 _tokenId = tokenIdsDelegateds[i];
        /** @dev Check if is really delegated, if not ve voting power for this tokenId is 0. */
        if (_user == _lockingPositionDelegate.delegatedVeCvg(_tokenId)) {
            _totalVotingPower += balanceOfVeCvg(_tokenId);
        }

        unchecked {
            ++i;
        }
    }
}
```

3. As stated by the developer, `manageOwnedAndDelegated` was created to help avoid OOG errors by caching the results of delegation evaluation. This assumption is likely made on the premise that real one-to-one token ownership would act as a dampening mechanism to avoiding excessively-long loops. However, since this exploit is not sufficiently constrained by scarce resource, a malicious user can re-introduce the feasibility of OOG reversion through alternatively using excessively long arrays.

## Code Snippet

```solidity
/**
 * @notice Allow a user to manage the tokens id (owned and delegated) used to represent their voting power.
 * @dev This prevents bad actors who will spam an address by transferring or delegating a lot of VE/MG positions.
 * | This will prevent the oog when the voting/metagovernance power is calculated.
 * @param _ownedAndDelegatedTokens array of owned/veDelegated/mgDelegated tokenIds allowed
 */
function manageOwnedAndDelegated(OwnedAndDelegated calldata _ownedAndDelegatedTokens) external {
    /** @dev Clear the struct owneds and delegateds tokenId allowed for this user.*/
    delete tokenOwnedAndDelegated[msg.sender];

    /** @dev Add new owned tokenIds allowed for this user.*/
    for (uint256 i; i < _ownedAndDelegatedTokens.owneds.length;) {
        /** @dev Check if tokenId is owned by the user.*/
        require(
            msg.sender == cvgControlTower.lockingPositionManager().ownerOf(_ownedAndDelegatedTokens.owneds[i]),
            "TOKEN_NOT_OWNED"
        );
        tokenOwnedAndDelegated[msg.sender].owneds.push(_ownedAndDelegatedTokens.owneds[i]);
        unchecked {
            ++i;
        }
    }
    /** @dev Add new mgCvg delegated tokenIds allowed for this user.*/
    for (uint256 i; i < _ownedAndDelegatedTokens.mgDelegateds.length;) {
        /** @dev Check if the user is a mgCvg delegatee for this tokenId.*/
        (, , uint256 _toIndex) = getMgDelegateeInfoPerTokenAndAddress(
            _ownedAndDelegatedTokens.mgDelegateds[i],
            msg.sender
        );
        require(_toIndex != 999, "NFT_NOT_MG_DELEGATED");
        tokenOwnedAndDelegated[msg.sender].mgDelegateds.push(_ownedAndDelegatedTokens.mgDelegateds[i]);
        unchecked {
            ++i;
        }
    }
    /** @dev Add new veCvg delegated tokenIds allowed for this user.*/
    for (uint256 i; i < _ownedAndDelegatedTokens.veDelegateds.length;) {
        /** @dev Check if the user is the veCvg delegatee for this tokenId.*/
        require(msg.sender == delegatedVeCvg[_ownedAndDelegatedTokens.veDelegateds[i]], "NFT_NOT_VE_DELEGATED");
        tokenOwnedAndDelegated[msg.sender].veDelegateds.push(_ownedAndDelegatedTokens.veDelegateds[i]);
        unchecked {
            ++i;
        }
    }
}
```

## Tool used

Manual Review, Visual Studio Code, GitHub

## Recommendation

Consider using an `EnumerableSet` instead of an `uint256[]` to prevent the existence of duplicates.

